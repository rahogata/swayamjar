plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'groovy'
    id 'eclipse'
    id 'maven-publish'
}

version = artifactVersion
group = 'in.co.rahogata'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5',
                        gradleTestKit()
}

gradlePlugin {
    plugins {
        swayamjar {
            id = 'in.co.rahogata.swayamjar'
            implementationClass = 'in.co.rahogata.swayamjar.SwayamjarPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/rahogata/swayamjar'
    vcsUrl = 'https://github.com/rahogata/swayamjar'
    description = 'A Gradle plugin to create an executable shell/batch script and embedding JAR in it.'
    tags = ['executable jar', 'shell', 'batch']
}

sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

task copyTestResources(type: Copy) {
	from 'src/test/resources/Main.jar'
	into 'build/functionalTest'
}

task functionalTest(type: Test, dependsOn: copyTestResources) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    dependsOn(tasks.functionalTest)
}

javadoc {
	enabled = false
}

